拓展包 'fkp复刻标包'

$ '奸雄' "当你受到伤害时，你可以获得造成伤害的牌。"
  触发技
  时机: 受到伤害后
  条件:
    若'造成伤害的牌'不是不存在的，
      且你是'伤害目标'，
      且(你拥有技能"奸雄")，则
      返回真
    以上。
  效果: 你获得牌'造成伤害的牌'。
以上

# 魏 "魏武帝" '曹操' 4
  ["奸雄"]

-------------------------------------------------

$ '刚烈' "你受到伤害后，可以令伤害来源进行判定，若结果不为红桃，则伤害来源需弃置两张手牌，否则你对其造成一点伤害。"
  触发技
  时机: 受到伤害后
  条件: 若你是'伤害目标'，且'伤害来源'不是不存在的，且(你拥有技能"刚烈")，则返回真，以上。
  效果:
    令'判定牌'为(你进行判定: (
      '技能名': "刚烈",
      '判定规则': ('创建卡牌规则'(
        '花色表': ['红桃']
      )),
      '希望判定中': 假
    ))，
    若'判定牌'的属性"花色"不是'红桃'，则
      令'弃置的牌'为('伤害来源'弃置2张牌: (
        '可以弃装备': 假,
        '可以点取消': 真,
        '提示信息': ('创建提示信息' (
          '文本': "%src 发动了“刚烈”，请弃置 %arg 张牌，否则你将受到 1 点伤害",
          '玩家1': 你,
          '变量1': 2
        ))
      ))
      若'弃置的牌'的属性"长度"是0，则
        你对'伤害来源'造成1点伤害。
      以上。
    以上。
以上

# 魏 "独眼罗刹" '夏侯惇' 4
  ["刚烈"]

-------------------------------------------------

$ '裸衣' "摸牌阶段，你可以少摸一张牌，若如此做，你本回合【杀】【决斗】造成的伤害+1。"
  触发技
  时机: 摸牌阶段摸牌时
  效果: 令'摸牌数量'为'摸牌数量'- 1

  时机: 造成伤害时
  条件:
    若(你本回合发动过"裸衣"的次数)大于0，
      且('造成伤害的牌'不是不存在的
        且('造成伤害的牌'的属性"牌名"是'杀'
          或'造成伤害的牌'的属性"牌名"是'雷杀'
          或'造成伤害的牌'的属性"牌名"是'火杀'
          或'造成伤害的牌'的属性"牌名"是'决斗'
        )
      )，则
      返回真
    以上
  消耗: 返回真
  效果: 令'伤害值'为'伤害值'+1。
以上

# 魏 "虎痴" '许褚' 4
  ["裸衣"]

-------------------------------------------------

$ '突袭' "摸牌阶段，你可以放弃摸牌并选择1-2名有手牌的角色，抽取其一张手牌。"
  触发技
  时机: 摸牌阶段摸牌时
  条件:
    若你不是'当前目标'，或(你拥有技能"突袭")是假，则返回假，以上。
    令'可选列表'为'其他角色'，
    对'可选列表'中每个'p'重复此流程:
      若'p'的属性"手牌数"是0，则
        从'可选列表'删除'p'
      以上
    以上
    若'可选列表'的属性"长度"大于0，则
      返回真，
    以上
  效果: 
    令'摸牌数量'为0。
    令'可选列表'为'其他角色'，
    对'可选列表'中每个'p'重复此流程:
      若'p'的属性"手牌数"是0则从'可选列表'删除'p'，以上
    以上
    若'可选列表'的属性"长度"大于0，则
      令'对象2'为不存在的
      令'对象1'为(你从'可选列表'选择一名角色: (
        '可以点取消': 假
      ))
      从'可选列表'删除'对象1'
      若'可选列表'的属性"长度"大于0，则
        令'对象2'为(你从'可选列表'选择一名角色)
      以上
      令'牌'为(你选择'对象1'一张牌: ('位置': ['手牌区']))
      你获得牌'牌'
      若'对象2'不是不存在的，则
        令'牌2'为(你选择'对象2'一张牌: ('位置': ['手牌区']))
        你获得牌'牌2'
      以上
    以上
以上

# 魏 "前将军" '张辽' 4
  ["突袭"]

-------------------------------------------------

$ '鬼才' "任意角色的判定牌生效前，你可以打出一张手牌代替之。"
  触发技
  时机: 改判前
  条件: 若(你拥有技能"鬼才")且你的属性"手牌数"大于0，则返回真，以上。
  消耗：
    令'改判的牌'为(你打出一张牌: (
      '提示': ('创建提示信息'('文本': "请使用“鬼才”来打出一张牌替换当前判定牌")),
      '是否为改判': 真
    ))
    若'改判的牌'不是不存在的，则
      令‘消耗数据’为‘改判的牌’
      返回真
    以上
  效果:
    令‘改判的牌’为‘消耗数据’作为卡牌类型
    你将判定结果修改为'改判的牌'
以上

$ '反馈' "当你受到伤害时，你可以抽取伤害来源的一张牌。"
  触发技
  时机: 受到伤害后
  条件: 若你是'伤害目标'，且'伤害来源'不是不存在的，且(你拥有技能"反馈")，则返回真，以上。
  效果:
    令'牌'为(你选择'伤害来源'一张牌: (
      '位置': ['手牌区', '装备区', '判定区']
    ))
    你获得牌'牌'
以上

# 魏 "狼顾之鬼" '司马懿' 3
  ["鬼才", "反馈"]

-------------------------------------------------

$ '天妒' "你的判定牌生效后，你可以获得之。" 默认技
  触发技
  时机: 判定完成后
  条件: 若'判定角色'是你且(你拥有技能"天妒")，则返回真，以上。
  效果: 你获得牌'判定牌'。
以上

注：$ '遗计' "当你受到伤害后，你可以摸两张牌并分发给任意角色。"
注：  触发技
注：  时机: 受到伤害后
注：  条件: 若你是'伤害目标'，且(你拥有技能"遗计")，则返回真，以上。
注：  效果: 令'数量'为0，
注：        重复此流程:
注：          若(你选择发动"遗计")，则
注：            令'拿来的牌'为(你选择牌堆顶2张牌('是否不放回': 真))
注：            若'拿来的牌'的属性"长度"大于0，则
注：              对'拿来的牌'中每个'牌'重复此流程:
注：                令'给出对象'为(你从'其他角色'选择一名角色('提示框文本': ('创建提示信息' (
注：                  '文本': "是否将 %arg 交给一名其他角色？",
注：                  '变量1': '牌'
注：                ))))
注：                若'给出对象'不是不存在的，则
注：                  '给出对象'获得牌'牌'。
注：                否则，你获得牌'牌'。
注：                以上。
注：              以上。
注：            以上。
注：          以上。
注：          令'数量'为'数量'+1。
注：       直到 '数量'不小于'伤害值'
注： 以上

# 魏 "早终的先知" '郭嘉' 3
  ["天妒"]

-------------------------------------------------

$ '洛神' "准备阶段，你可以进行判定，若判定结果为黑色，你获得之，然后你可以继续判定，直到出现红色的判定牌为止。"
  触发技
  时机: 阶段开始时
  条件:
    若你的属性"当前阶段"是'准备阶段'，
      且你是'当前目标'，
      且(你拥有技能"洛神")，则
      返回真
    以上。
  效果:
    令'判定牌'为不存在的。
    重复此流程:
      若(你选择发动"洛神")则
        令'判定牌'为(你进行判定: (
          '判定规则': ('创建卡牌规则'(
            '花色表': ['梅花','黑桃']
          )),
          '希望判定中': 真
        ))
      否则
        中止此流程
      以上
    直到'判定牌'的属性"花色"是'红桃'或'判定牌'的属性"花色"是'方块'。

  时机: 判定完成后
  条件:
    若(你拥有技能"洛神")，
      且'判定角色'是你且'判定原因'是"洛神"
      且('判定牌'的属性"花色"是'黑桃'或'判定牌'的属性"花色"是'梅花')，则
      返回真，
    以上。
  消耗: 返回真
  效果: 你获得牌'判定牌'。
以上

$ '倾国' "可以将黑色牌当闪使用打出"
  视为技
  条件: 返回假
  选牌规则:
    若'已选卡牌'的属性"长度"是0，则
      返回'备选卡牌'的属性"花色"是'黑桃'
        或'备选卡牌'的属性"花色"是'梅花'
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，则返回真，以上。
  视为规则: 返回('创建虚拟牌'(
    '牌名': '闪',
    '子卡牌': '选择的卡牌',
  ))
  响应条件: 返回真
  响应规则: [('创建卡牌规则'(
    '牌名表': ['闪'],
    '花色表': ['黑桃', '梅花']
  ))]
以上

# 魏 "薄幸的美人" '甄姬' 3 女性
  ["洛神", "倾国"]

-------------------------------------------------

$ '仁德' "出牌阶段，你可以将任意手牌交给其他角色。若一回合你以此法交出超过两张手牌，你恢复一点体力。"
  主动技
  条件: 返回真
  选牌规则: 返回真
  选目标规则: 若'备选目标'不是你且'已选目标'的属性"长度"是0，则返回真，以上。
  可以点确定: 若'已选卡牌'的属性"长度"大于0且'已选目标'的属性"长度"大于0，则返回真，以上。
  使用后:
    令'小于2'为假。
    若(你的"已仁德"隐藏标记数量)小于2，则令'小于2'为真，以上。
    令'目标'为('选择的目标'第1个元素)，你交给'目标'牌'选择的卡牌'。
    令X为('选择的卡牌'的属性"长度")。
    你获得X枚"已仁德"隐藏标记。
    若'小于2'且(你的"已仁德"隐藏标记数量)大于2，则你回复1点体力。以上。

  触发技
  时机: 阶段结束时
  条件: 若你的属性"当前阶段"是'出牌阶段'，且你是'当前目标'，则返回真，以上。
  消耗: 返回真
  效果: 令X为(你的"已仁德"隐藏标记数量)，你失去X枚"已仁德"隐藏标记。
以上

# 蜀 "乱世的枭雄" '刘备' 4
  ["仁德"]

-------------------------------------------------

$ '武圣' "威震华夏的武圣关云长拥有将红牌当杀的神技。"
  视为技
  条件: 返回真
  选牌规则:
    若'已选卡牌'的属性"长度"是0，则
      返回'备选卡牌'的属性"花色"是'红桃'或'备选卡牌'的属性"花色"是'方块'，
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，则返回真，以上。
  视为规则: 返回('创建虚拟牌'(
     '牌名': '杀',
     '子卡牌': '选择的卡牌'
   ))
  响应条件: 返回真
  响应规则: [('创建卡牌规则'(
    '牌名表': ['杀'],
    '花色表': ['红桃','方块']
  ))]
以上

# 蜀 "美髯公" '关羽' 4
  ["武圣"]

-------------------------------------------------

$ '咆哮' "锁定技，你使用【杀】无次数限制。" 锁定技
  状态技
  出牌次数修正:
    若('玩家'拥有技能"咆哮")，
      且('卡牌'的属性"牌名"是'杀'或'卡牌'的属性"牌名"是'雷杀'或'卡牌'的属性"牌名"是'火杀')，则
      返回999
    以上。
以上

# 蜀 "万夫不当" '张飞' 4
  ["咆哮"]

-------------------------------------------------

$ '龙胆' "你可以将一张【杀】当【闪】使用或打出，或将一张【闪】当普通【杀】使用或打出。"
  视为技
  条件: 返回真
  选牌规则:
    若'已选卡牌'的属性"长度"是0，则
      令'名字'为'备选卡牌'的属性"牌名"，
      返回'名字'是'杀'或'名字'是'雷杀'或'名字'是'火杀'
        或'名字'是'闪'
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，则返回真，以上。
  视为规则:
    令'name'为('选择的卡牌'第1个元素)的属性"牌名"，
    若'name'是'闪'，则
      返回('创建虚拟牌'(
        '牌名': '杀',
        '子卡牌': '选择的卡牌'
      ))
    否则
      返回('创建虚拟牌'(
        '牌名': '闪',
        '子卡牌': '选择的卡牌'
      ))
    以上。
  响应条件: 返回真
  响应规则: ['杀', '闪']
以上

# 蜀 "少年将军" '赵云' 4
  ["龙胆"]

-------------------------------------------------

$ '观星' "回合开始前，你可以观看牌堆顶X张牌(X为当前存活角色数量且最大为5)，并按任意顺序将其放置在牌堆顶或牌堆底。"
  触发技
  时机: 阶段开始时
  条件:
    若你的属性"当前阶段"是'准备阶段'，且你是'当前目标'，且(你拥有技能"观星")则
      返回 真
    以上
  效果:
    令X为'其他角色'的属性"长度"。
    若X大于5则令X为5，以上，
    令'观星的牌'为(你选择牌堆顶X张牌:('是否不放回': 假))，
    你对'观星的牌'进行观星:('观星类型': '只放置顶部')
以上

$ '空城' "锁定技，若你没有手牌，则你不是【杀】【决斗】的合法目标，以上。" 锁定技
  状态技
  禁止指定目标:
    若('目标'拥有技能"空城")且'目标'的属性"手牌数"是0，
      且('卡牌'的属性"牌名"是'杀'
        或'卡牌'的属性"牌名"是'火杀'
        或'卡牌'的属性"牌名"是'雷杀'
        或'卡牌'的属性"牌名"是'决斗')，则
      返回真，
    以上。
以上

# 蜀 "迟暮的丞相" '诸葛亮' 3
  ["观星", "空城"]

-------------------------------------------------

$ '马术' "锁定技，你计算到其他人的距离-1。" 锁定技
  状态技
  距离修正: 若('来源'拥有技能"马术")，则返回-1，以上。
以上

# 蜀 "一骑当先" '马超' 4
  ["马术"]

-------------------------------------------------

$ '集智' "当你使用一张锦囊牌时，你可以摸一张牌。"
  触发技
  时机: 卡牌使用后
  条件: 若(你拥有技能"集智")且'使用者'是你且'使用的牌'的属性"类别"是'锦囊牌'，则返回真，以上。
  效果: 你摸1张牌
以上

$ '奇才' "锁定技，你使用锦囊牌无距离限制。"
  状态技
  出牌距离修正:
    若('玩家'拥有技能"奇才")，
      且('卡牌'的属性"类别"是'锦囊牌')，则
      返回999
    以上。
以上

# 蜀 "归隐的才女" '黄月英' 3 女性
  ["集智", "奇才"]

-------------------------------------------------

$ '制衡' "出牌阶段限一次，你可以弃置任意张手牌，然后摸等量的手牌。"
  主动技
  条件: 若(你本阶段发动过"制衡"的次数)不大于0，则返回真，以上。
  选牌规则: 返回真
  选目标规则: 返回假
  可以点确定: 若'已选卡牌'的属性"长度"大于0，则返回真，以上。
  使用后:
    你因技能"制衡"弃置卡牌'选择的卡牌'。
    你摸'选择的卡牌'的属性"长度"张牌。
以上

# 吴 "年轻的贤君" '孙权' 4
  ["制衡"]

-------------------------------------------------

$ '英姿' "摸牌阶段，你可以多摸一张牌。" 默认技
  触发技
  时机: 摸牌阶段摸牌时
  效果: 令'摸牌数量'为'摸牌数量'+1。
以上

$ '反间' "出牌阶段限一次，你可以令一名角色选择一个花色，然后其获得你一张手牌并展示之。若其猜错该手牌，其受到你的1点伤害。"
  主动技
  条件: 若(你本阶段发动过"反间"的次数)不大于0且你的属性"手牌数"大于0，则返回真，以上。
  选牌规则: 返回假
  选目标规则: 若'已选目标'的属性"长度"是0，且'备选目标'不是你，则返回真，以上。
  可以点确定: 若'已选目标'的属性"长度"大于0，则返回真，以上。
  使用后:
    令'目标'为('选择的目标'第1个元素)。
    令'选项'为['红桃','方块','黑桃','梅花']，
    令'选择结果'为('目标'从'选项'选择一项)，
    令'牌'为('目标'选择你一张牌: (
      '位置': ['手牌区']
    ))
    '目标'获得牌'牌'
    注：展示牌
    若'牌'的属性"花色"不是'选项'，则你对'目标'造成1点伤害，以上。
以上

# 吴 "大都督" '周瑜' 3
  ["英姿", "反间"]

-------------------------------------------------

$ '克己' "若你出牌阶段没有出【杀】，则你可以跳过本回合的弃牌阶段。"
  触发技
  时机: 卡牌使用后
  条件:
    若(你拥有技能"克己")
      且'使用者'是你
      且('使用的牌'的属性"牌名"是'杀'
        或'使用的牌'的属性"牌名"是'雷杀'
        或'使用的牌'的属性"牌名"是'火杀')则
      返回真
    以上。
  消耗: 返回真
  效果: 你获得1枚"克己"隐藏标记。

  时机: 阶段开始时
  条件:
    若你不是'当前目标'，则返回假，以上
    若(你的属性"当前阶段"是'结束阶段'或你的属性"当前阶段"是'开始阶段')且(你拥有技能"克己")，则
      令X为(你的"克己"隐藏标记数量)
      你失去X枚"克己"隐藏标记
      返回假
    以上。
    若(你的属性"当前阶段"是'弃牌阶段')，且(你的"克己"隐藏标记数量)是0，则
      返回真
    以上
  效果: 返回真
以上

# 吴 "白衣渡江" '吕蒙' 4
  ["克己"]

-------------------------------------------------

$ '谦逊' "锁定技，你不是【顺手牵羊】【乐不思蜀】的合法目标。"
  状态技
  禁止指定目标:
    若('目标'拥有技能"谦逊")
      且('卡牌'的属性"牌名"是'乐不思蜀'或'卡牌'的属性"牌名"是'顺手牵羊')，则
      返回真
    以上。
以上

$ '连营' "当你失去最后的手牌时，你可以摸一张牌。" 默认技
  触发技
  时机: 卡牌移动后
  条件:
    若(你拥有技能"连营")
      且'移动的来源'是你
      且'是最后的手牌'是真
      且你的属性"手牌数"是0，则
      返回真
    以上。
  效果: 你摸1张牌
以上

# 吴 "儒生雄才" '陆逊' 4
  ["谦逊", "连营"]

-------------------------------------------------

$ '奇袭' "你可以将黑色牌当作【过河拆桥】使用。"
  视为技
  条件: 返回真
  选牌规则: 若'已选卡牌'的属性"长度"是0，则
      返回'备选卡牌'的属性"花色"是'黑桃'或'备选卡牌'的属性"花色"是'梅花'。
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，则返回真，以上。
  视为规则:
    返回('创建虚拟牌'(
      '牌名': '过河拆桥',
      '子卡牌': '选择的卡牌',
      '技能名': "奇袭"
    ))
  响应条件: 返回真
  响应规则: [('创建卡牌规则'(
    '牌名表': ['过河拆桥'],
    '花色表': ['梅花', '黑桃']
  ))]
以上

# 吴 "锦帆游侠" '甘宁' 4
  ["奇袭"]

-------------------------------------------------

注：TODO: 更正流离
$ '流离' "当你成为杀的目标时，你可以弃一张牌摸一张牌。"
  触发技
  时机: 指定目标时
  条件: 若'目标'是你，且(你拥有技能"流离")，且'使用的牌'的属性"牌名"是'杀'，则返回真，以上。
  效果:
    若(你选择发动"流离")，则你弃置1张牌，
    然后令'可选目标'为'其他角色'，
    对'可选目标'中每个'player'重复此流程:
      若'player'是'使用者'则从'可选目标'删除'player'，以上。
    以上，
    令'新目标'为(你从'可选目标'选择一名角色)，
    令'目标'为'新目标'。
    以上。
以上

$ '国色' "你可以将方片牌当作【乐不思蜀】使用。"
  视为技
  条件: 返回真
  选牌规则: 若'已选卡牌'的属性"长度"是0，则
      返回'备选卡牌'的属性"花色"是'方块'。
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，则返回真，以上。
  视为规则:
    返回('创建虚拟牌'(
      '牌名': '乐不思蜀',
      '子卡牌': '选择的卡牌'
    ))
以上

# 吴 "矜持之花" '大乔' 3 女性
  ["国色", "流离"]

-------------------------------------------------

注：TODO: 更正：move.from_place是nil，其实是from_places，类型为TNumberList，与move.cards对应
$ '枭姬' "当你失去一张装备牌时，你可以摸两张牌。" 默认技
  触发技
  时机: 卡牌移动后
  条件: 若(你拥有技能"枭姬")且'移动来源地'是'装备区'且'移动的来源'是你，则返回真，以上。
  效果: 你摸2张牌。
以上

$ '结姻' "出牌阶段限一次，你可以弃置两张手牌并选择一名已受伤的男性角色：若如此做，你和该角色各回复1点体力。"
  主动技
  条件: 若(你本阶段发动过"结姻"的次数)是0且你的属性"手牌数"不小于2，则返回真，以上。
  选牌规则: 若'已选卡牌'的属性"长度"小于2，则返回真，以上。
  选目标规则:
    若'已选目标'的属性"长度"是0，
      注：且'备选目标'不是你，
      且'备选目标'的属性"性别"是'男性'，
      且'备选目标'的属性"体力值"小于'备选目标'的属性"体力上限"，则
      返回真
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是2，且'已选目标'的属性"长度"是1，则返回真，以上。
  使用后:
    你因技能"结姻"弃置卡牌'选择的卡牌'，
    然后若你的属性"体力值"小于你的属性"体力上限"，则你回复1点体力，以上，
    令'目标'为('选择的目标'第1个元素)，
    若'目标'的属性"体力值"小于'目标'的属性"体力上限"，则'目标'回复1点体力。以上。
以上

# 吴 "弓腰姬" '孙尚香' 3 女性
  ["枭姬", "结姻"]

-------------------------------------------------

$ '苦肉' "出牌阶段，你可以失去1点体力，摸2张牌。"
  主动技
  条件: 返回真
  选牌规则: 返回假
  选目标规则: 返回假
  可以点确定: 返回真
  使用后: 你失去1点体力，然后你摸2张牌。
以上

# 吴 "轻身为国" '黄盖' 4
  ["苦肉"]

-------------------------------------------------

$ '青囊' "出牌阶段限一次，你可以弃置一张手牌并选择一名已受伤的角色，若如此做，该角色回复1点体力。"
  主动技
  条件: 若(你本阶段发动过"青囊"的次数)是0且你的属性"手牌数"不小于1，则返回真，以上。
  选牌规则: 若'已选卡牌'的属性"长度"小于1，则返回真，以上。
  选目标规则: 若'已选目标'的属性"长度"是0，且'备选目标'的属性"体力值"小于'备选目标'的属性"体力上限"，则返回真，以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，且'已选目标'的属性"长度"是1，则返回真，以上。
  使用后:
    你因技能"青囊"弃置卡牌'选择的卡牌'，
    令'目标'为('选择的目标'第1个元素)，
    若'目标'的属性"体力值"小于'目标'的属性"体力上限"，则'目标'回复1点体力。以上。
以上

$ '急救' "你的回合外，你可以将一张红色牌当【桃】使用。 "
  视为技
  条件: 若(你拥有技能"青囊")且你的属性"当前阶段"是'回合外'，则返回真，以上。
  选牌规则:
    若'已选卡牌'的属性"长度"是0，则
      返回'备选卡牌'的属性"花色"是'方块'或'备选卡牌'的属性"花色"是'红桃'。
    以上。
  可以点确定: 若'已选卡牌'的属性"长度"是1，则返回真，以上。
  视为规则:
    返回('创建虚拟牌'(
      '牌名': '桃',
      '子卡牌': '选择的卡牌',
    ))
  响应条件: 返回真
  响应规则: [('创建卡牌规则'(
    '牌名表': ['桃'],
    '花色表': ['红桃','方块']
  ))]
以上

# 群 "神医" '华佗' 3
  ["青囊", "急救"]

-------------------------------------------------

$ '闭月' "你的结束阶段，你可以摸一张牌。" 默认技
  触发技
  时机: 阶段开始时
  条件:
    若你是'当前目标'
      且(你拥有技能"闭月")
      且你的属性"当前阶段"是'结束阶段'，则
      返回真
    以上。
  效果: 你摸1张牌。
以上

# 群 "绝世的舞姬" '貂蝉' 3 女性
  ["闭月"]
