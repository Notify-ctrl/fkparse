$ '奸雄' "当你受到伤害时，你可以获得造成伤害的牌"
  触发技
  时机: 受到伤害后
  条件: 若'造成伤害的牌'不是不存在的且你是'伤害目标'，且(你拥有技能"奸雄")，则返回真，否则返回假，以上。
  效果: 若(你选择发动"奸雄")，则你说出"nosjianxiong"的台词，然后你获得卡牌'造成伤害的牌'，以上。

$ '刚烈' "你受到伤害后，可以令伤害来源进行判定，若结果不为红桃，则伤害来源需弃置两张手牌，否则你对其造成一点伤害。"
  触发技
  时机: 受到伤害后
  条件: 若你是'伤害目标'，且'伤害来源'不是不存在的，且(你拥有技能"刚烈")，则返回真，以上。
  效果: 若(你选择发动"刚烈")，则
          你说出"nosganglie"的台词   注：nosganglie就是神杀自带的标刚烈，偷个语音用用
          令'判定牌'为(你进行判定{
            '技能名': "刚烈",
            '判定规则': (调用'创建卡牌规则'{
              '花色表': ['红桃']
            }),
            '希望判定中': 假
          })，
          若'判定牌'的"花色"不是'红桃'，则
            令'弃置的牌'为('伤害来源'弃置2张牌{
              '可以弃装备':假,
              '可以点取消':真,
              '提示信息': (调用'创建提示信息' {
                '文本': "%src 发动了“刚烈”，请弃置 %arg 张牌，否则你将受到 1 点伤害",
                '玩家1': 你,
                '变量1': 2
              })
            })
            若'弃置的牌'的"长度"是0，则
              你对'伤害来源'造成1点伤害。
            以上。
          以上。
        以上。

$ '裸衣' "摸牌阶段，你可以少摸一张牌，若如此做，你本回合【杀】【决斗】造成的伤害+1。"
  触发技
  时机: 摸牌阶段摸牌时
  效果: 若(你选择发动"裸衣")，则你说出"nosluoyi"的台词，令'摸牌数量'为'摸牌数量'-1，然后你获得1枚"裸衣"标记，以上
  时机: 造成伤害时
  条件: 若(你拥有技能"裸衣")，且(你"裸衣"标记数量)大于0，且('造成伤害的牌'不是不存在的且('造成伤害的牌'的"牌名"是'杀'或'造成伤害的牌'的"牌名"是'决斗'))，则返回真，以上
  注：牌名应当用单引号''括起
  效果: 你说出"nosluoyi"的台词，然后令'伤害值'为'伤害值'+1。
  时机: 阶段结束时
  条件: 若(你"裸衣"标记数量)大于0，且你的"当前阶段"是'结束阶段'，且(你拥有技能"裸衣")，则返回真，以上
  效果: 令X为(你"裸衣"标记数量)，你失去X枚"裸衣"标记。

$ '突袭' "摸牌阶段，你可以放弃摸牌并选择1-2名有手牌的角色，抽取其一张手牌。"
  触发技
  时机: 摸牌阶段摸牌时
  效果: 若(你选择发动"突袭")，则
         你说出"nostuxi"的台词，
         令'摸牌数量'为0。
         令'可选列表'为'其他角色'，
         对'可选列表'中每个'p'重复此流程:
           若'p'的"手牌数"是0则从'可选列表'删除'p'，以上
         以上
         若'可选列表'的"长度"大于0，则
           令'对象2'为不存在的
           令'对象1'为(你从'可选列表'选择一名角色)
           从'可选列表'删除'对象1'
           若'可选列表'的"长度"大于0，则
             令'对象2'为(你从'可选列表'选择一名角色)
           以上
           令'牌'为(你选择'对象1'一张牌{'位置': ['手牌区']})
           你获得牌'牌'
           若'对象2'不是不存在的，则
             令'牌2'为(你选择'对象2'一张牌{'位置': ['手牌区']})
             你获得牌'牌2'
           以上
         以上
       以上

$ '反馈' "当你受到伤害时，你可以抽取伤害来源的一张牌。"
  触发技
  时机: 受到伤害后
  条件: 若你是'伤害目标'，且'伤害来源'不是不存在的，且(你拥有技能"反馈")，则返回真，以上。
  效果: 若(你选择发动"反馈")，则
    你说出"nosfankui"的台词。
    令'牌'为(你选择'伤害来源'一张牌{
      '位置': ['手牌区', '装备区', '判定区']
    })
    你获得牌'牌'
  以上。

$ '鬼裁' "任意角色的判定牌生效前，你可以打出一张牌代替之。"
  触发技
  时机: 改判前
  条件: 若(你拥有技能"鬼裁")且你的"手牌数"大于0，则返回真，否则返回假，以上。
  效果: 若(你选择发动"鬼裁")，则
         令'改判的牌'为(你打出一张牌{'是否为改判': 真})。
         你说出"nosguicai"的台词。
         你将判定结果修改为('改判的牌'){'技能名': "鬼裁", '是否交换': 假}
       以上

$ '天妒' "你的判定牌生效后，你可以获得之。"
  触发技
  时机: 判定完成后
  条件: 若'判定角色'是你且(你拥有技能"天妒")，则返回真，否则返回假，以上。
  效果: 你获得牌'判定牌'。

注：$ '遗计' "当你受到伤害后，你可以摸两张牌并分发给任意角色。"
注：  触发技
注：  时机: 受到伤害后
注：  条件: 若你是'伤害目标'，且(你拥有技能"遗计")，则返回真，以上。
注：  效果: 令'数量'为0，
注：        重复此流程:
注：          若(你选择发动"遗计")，则
注：            令'拿来的牌'为(你选择牌堆顶2张牌{'是否不放回': 真})
注：            若'拿来的牌'的"长度"大于0，则
注：              对'拿来的牌'中每个'牌'重复此流程:
注：                令'给出对象'为(你从'其他角色'选择一名角色{'提示框文本': (调用'创建提示信息' {
注：                  '文本': "是否将 %arg 交给一名其他角色？",
注：                  '变量1': '牌'
注：                })})
注：                若'给出对象'不是不存在的，则
注：                  '给出对象'获得牌'牌'。
注：                否则，你获得牌'牌'。
注：                以上。
注：              以上。
注：            以上。
注：          以上。
注：          令'数量'为'数量'+1。
注：       直到 '数量'不小于'伤害值'

$ '洛神' "准备阶段，你可以进行判定，若判定结果为黑色，你获得之，然后你可以继续判定，直到出现红色的判定牌为止。"
  触发技
  时机: 阶段开始时
  条件: 若你的"当前阶段"是'准备阶段'且(你拥有技能"洛神")，则返回真，否则返回假，以上。
  效果:
    令'判定牌'为不存在的。
    重复此流程:
      若(你选择发动"洛神")则
        令'判定牌'为(你进行判定{
          '判定规则': ( 调用'创建卡牌规则'{
            '花色表': ['梅花','黑桃']
            }
          ),
          '希望判定中': 真
        })
        注：若'判定牌'的"花色"是'黑桃'或'判定牌'的"花色"是'梅花'则你获得牌'判定牌'，以上。
      否则 终止此流程
      以上
    直到'判定牌'不是不存在的且('判定牌'的"花色"是'红桃'或'判定牌'的"花色"是'方块')。
  时机: 判定完成后
  条件: 若(你拥有技能"洛神")且'判定角色'是你且'判定原因'是"洛神"
       且('判定牌'的"花色"是'黑桃'或'判定牌'的"花色"是'梅花')，则
         返回真，
       否则返回假，以上。
  效果: 你获得牌'判定牌'。

$ '倾国' "可以将黑色牌当闪使用打出"
  视为技
  条件: 返回假
  选牌规则: 若'已选卡牌'的"长度"是0，则返回'备选卡牌'的"花色"是'黑桃'或'备选卡牌'的"花色"是'梅花'，以上。
  可以点确定: 若'已选卡牌'的"长度"是1，则返回真，以上。
  视为规则: 返回(调用'创建虚拟牌'{
    '牌名': '闪',
    '子卡牌': '选择的卡牌',
    '技能名': "倾国"
  })
  响应条件: 返回真
  响应规则: [(调用'创建卡牌规则'{
    '牌名表': ['闪'],
    '花色表': ['黑桃', '梅花']
  })]

$ '仁德' "出牌阶段，你可以将任意手牌交给其他角色。若一回合你以此法交出超过两张手牌，你恢复一点体力。"
  主动技
  条件: 返回真
  选牌规则: 返回真
  选目标规则: 若'备选目标'不是你且'已选目标'的"长度"是0，则返回真，以上。
  可以点确定: 若'已选卡牌'的"长度"大于0且'已选目标'的"长度"大于0，则返回真，以上。
  使用后:
    令'小于2'为假。
    若(你"已仁德"隐藏标记数量)小于2，则令'小于2'为真，以上。
    令'目标'为('选择的目标'第1个元素)，你交给'目标'牌'选择的卡牌'。
    令X为('选择的卡牌'的"长度")。
    你获得X枚"已仁德"隐藏标记。
    若'小于2'且(你"已仁德"隐藏标记数量)大于2，则你回复1点体力。以上。
  触发技
  时机: 阶段结束时
  效果: 若你的"当前阶段"是'出牌阶段'，则
    令X为(你"已仁德"隐藏标记数量)，你失去X枚"已仁德"隐藏标记。
  以上。

$ '武圣' "威震华夏的武圣关云长拥有将红牌当杀的神技。"
  视为技
  条件: 返回真
  选牌规则: 若'已选卡牌'的"长度"是0，
       则返回'备选卡牌'的"花色"是'红桃'或'备选卡牌'的"花色"是'方块'，
  以上。
  可以点确定: 若'已选卡牌'的"长度" 是1，则返回真，以上。
  视为规则: 返回(调用'创建虚拟牌'{
     '牌名': '杀',
     '子卡牌': '选择的卡牌',
     '技能名': "武圣"
   })
  响应条件: 返回真
  响应规则: [(
    调用'创建卡牌规则'{
    '牌名表': ['杀'],
    '花色表': ['红桃',方块']
   }
  )]

$ '咆哮' "锁定技，你使用【杀】无距离限制。"
  状态技
  出牌次数修正: 若('玩家'拥有技能"咆哮")，
      且('卡牌'的"牌名"是'杀'或'卡牌'的"牌名"是'雷杀'或'卡牌'的"牌名"是'火杀')，
      则 返回999，以上。   注：也就是把出牌次数+999

$ '龙胆' "你可以将一张【杀】当【闪】使用或打出，或将一张【闪】当普通【杀】使用或打出。"
  视为技
  条件: 返回真
  选牌规则: 若'已选卡牌'的"长度"是0，则
    令'名字'为'备选卡牌'的"牌名"，
    返回'名字'是'杀'或'名字'是'雷杀'或'名字'是'火杀'
      或'名字'是'闪'
  以上。
  可以点确定: 若'已选卡牌'的"长度"是1，则返回真，以上。
  视为规则:
    令'name'为('选择的卡牌'第1个元素)的"牌名"，
    若'name'是'闪'，则
      返回(调用'创建虚拟牌'{
        '牌名': '杀',
        '子卡牌': '选择的卡牌',
        '技能名': "龙胆"
      })
    否则
      返回(调用'创建虚拟牌'{
        '牌名': '闪',
        '子卡牌': '选择的卡牌',
        '技能名': "龙胆"
      })
    以上。
  响应条件: 返回真
  响应规则: ['杀', '闪']

$ '观星' "回合开始前，你可以观看牌堆顶X张牌(X为当前存活角色数量且最大为5)，并按任意顺序将其放置在牌堆顶或牌堆底。"
  触发技
  时机: 阶段开始时
  条件: 若你的"当前阶段"是'准备阶段'，且(你拥有技能"观星")则
           返回 真
         以上
  效果: 若(你选择发动"观星")，则
       令X为'其他角色'的"长度"+1。
       若X大于5则令X为5，以上，
       令'观星的牌'为(你选择牌堆顶X张牌{'是否不放回': 假})，
       你对'观星的牌'进行观星{'观星类型': '只放置顶部'}，以上。

$ '空城' "锁定技，若你没有手牌，则你不是【杀】【决斗】的合法目标，以上。"
  状态技
  禁止指定目标: 若('目标'拥有技能"空城")且'目标'的"手牌数"是0，且('卡牌'的"牌名"是'杀'或'卡牌'的"牌名"是'火杀'
              或'卡牌'的"牌名"是'雷杀'或'卡牌'的"牌名"是'决斗')，
                则返回真，
              以上。

$ '马术' "锁定技，你计算到其他人的距离-1。"
  状态技
  距离修正: 若('来源'拥有技能"马术")，则返回0-1，以上。

$ '集智' "当你使用一张锦囊牌时，你可以摸一张牌。"
  触发技
  时机: 卡牌使用后
  条件: 若(你拥有技能"集智")且'使用者'是你且'使用的牌'的"类别"是'锦囊牌'，则返回真，否则返回假，以上。
  效果: 若(你选择发动"集智")，则你摸1张牌，以上。

$ '奇才' "锁定技，你使用锦囊牌无距离限制。"
  状态技
  出牌距离修正: 若('玩家'拥有技能"奇才")，
      且('卡牌'的"类别"是'锦囊牌')，
      则 返回999，以上。

$ '制衡' "出牌阶段限一次，你可以弃置任意张手牌，然后摸等量的手牌。"
  主动技
  条件: 若(你发动主动技"制衡"的次数)不大于0，则返回真，以上。
  选牌规则: 返回真
  选目标规则: 返回假
  可以点确定: 若'已选卡牌'的"长度"大于0，则返回真，以上。
  使用后: 你因技能"制衡"弃置卡牌'选择的卡牌'。
  你摸'选择的卡牌'的"长度"张牌。

$ '英姿' "摸牌阶段，你多摸一张牌。"
  触发技
  时机:摸牌阶段摸牌时
  效果:令'摸牌数量'为'摸牌数量'+1。

$ '反间' "出牌阶段限一次，你可以令一名角色选择一个花色，然后其获得你一张手牌并展示之。若其猜错该手牌，其受到你的1点伤害。"
  主动技
  条件: 若(你发动主动技"反间"的次数)不大于0且你的"手牌数"大于0，则返回真，以上。
  选牌规则: 返回假
  选目标规则: 若'已选目标'的"长度"是0，且'备选目标'不是你，则返回真，以上。
  可以点确定: 若'已选目标'的"长度"大于0，则返回真，以上。
  使用后: 令'目标'为('选择的目标'第1个元素)。
    令'选项'为['红桃','方块','黑桃','梅花']，
    令'选择结果'为('目标'从'选项'选择一项)，
    令'牌'为('目标'选择你一张牌{
          '位置': ['手牌区']
        })
    '目标'获得牌'牌'
    若'牌'的"花色"不是'选项'，则你对'目标'造成1点伤害，以上。

$ '克己' "若你出牌阶段没有出【杀】，则你可以跳过本回合的弃牌阶段。"
  触发技
  时机: 卡牌使用后
  条件: 若(你拥有技能"克己")且'使用者'是你且('使用的牌'的"牌名"是'杀'
       或'使用的牌'的"牌名"是'雷杀'或'使用的牌'的"牌名"是'火杀')则返回真，否则返回假，以上。
  效果: 你获得1枚"克己"隐藏标记。
  时机: 阶段开始时
  效果: 若(你的"当前阶段"是'结束阶段'或你的"当前阶段"是'开始阶段')且(你拥有技能"克己")，
       则令X为(你"克己"隐藏标记数量)，你失去X枚"克己"隐藏标记。以上。
  时机: 阶段开始时
  注：时机: 阶段结束时
  效果: 若(你的"当前阶段"是'弃牌阶段')
  注：效果: 若(你的"当前阶段"是'出牌阶段')
       且(你拥有技能"克己")且(你"克己"隐藏标记数量)是0，则
        若(你选择发动"克己")，
        则返回真，以上。
        注：则跳过弃牌阶段，以上。
       以上。

$ '谦逊' "当你失去最后的手牌时，你摸一张牌。"
  触发技
  时机: 移动卡牌后
  条件: 若(你拥有技能"谦逊")且你的"手牌数"是0，则返回真，以上。
  效果: 若(你选择发动"谦逊")，则你摸1张牌，以上。

拓展包 'fkp复刻标包'

# 魏 "魏武帝" '曹操' 4 男性 {fkp_caocao}
  ["奸雄"]

# 魏 "独眼罗刹" '夏侯惇' 4 男性 {fkp_xiahoudun}
  ["刚烈"]

# 魏 "虎痴" '许褚' 4 男性 {fkp_xuchu}
  ["裸衣"]

# 魏 "前将军" '张辽' 4 男性 {fkp_zhangliao}
  ["突袭"]

# 魏 "狼顾之鬼" '司马懿' 3 男性 {fkp_simayi}
  ["反馈" "鬼裁"]

# 魏 "早终的先知" '郭嘉' 3 男性 {fkp_guojia}
  ["天妒"]

# 魏 "薄幸的美人" '甄姬' 3 女性 {fkp_zhenji}
  ["倾国" "洛神"]

# 蜀 "乱世的枭雄" '刘备' 4 男性 {fkp_liubei}
  ["仁德"]

# 蜀 "美髯公" '关羽' 4 男性 {fkp_guanyu}
  ["武圣"]

# 蜀 "万夫不当" '张飞' 4 男性 {fkp_zhangfei}
  ["咆哮"]

# 蜀 "少年将军" '赵云' 4 男性 {fkp_zhaoyun}
  ["龙胆"]

# 蜀 "迟暮的丞相" '诸葛亮' 3 男性 {fkp_zhugeliang}
  ["观星" "空城"]

# 蜀 "一骑当先" '马超' 4 男性 {fkp_machao}
  ["马术"]

# 蜀 "归隐的才女" '黄月英' 3 女性 {fkp_huangyueying}
  ["集智" "奇才"]