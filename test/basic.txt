定义函数 '自定义函数'
  {
    '玩家': 玩家类型,
    '血量': 数字类型,
    '数量': 数字类型 = 5
  }
  若 '玩家'的"体力值"小于'血量'则
    '玩家'摸'数量'张牌
  以上

定义函数 'test2' {'a':数字类型=2,'b':数字类型} -> 数字类型
  返回 'a' + 'b'
  注：你摸一张牌
  注：返回语句后不允许写新的语句，它标志着语句块的结束

$ '奸雄' "锁定技。当你造成伤害后，若你的体力值大于3，你摸5张牌；当你受到伤害后，你对伤害来源造成一点伤害，重复此流程直到伤害来源的体力值小于你的体力值；你造成的伤害+2。"
  锁定技 触发技
  时机:造成伤害后
  效果:若你的"体力值"大于3，则你摸5张牌，以上。

  时机:受到伤害后
  效果:重复此流程:你对'伤害来源'造成1点伤害。直到'伤害来源'的"体力值"小于你的"体力值"。

  时机:造成伤害时
  效果:令'伤害值'为'伤害值'+2。调用'自定义函数'{'玩家':你,'血量':2}
  令X为(调用'test2'{'b':4})
  对[你, '伤害来源', '伤害目标']中每个'p'重复此流程:'p'摸1张牌。以上。

$ '英姿' "摸牌阶段，你多摸一张牌。" 锁定技 {test_yingzi}
  触发技
  时机:摸牌阶段摸牌时
  效果:令'摸牌数量'为'摸牌数量'+1。
      令'选项'为["draw1damage1","draw2"]，
      令'选择结果'为(你从'选项'选择一项)，
      若'选择结果'是"draw1damage1"则
        你摸1张牌
        你对(你从'其他角色'选择一名角色)造成2点伤害
      否则
        若'选择结果'是"draw2"则
          你摸2张牌
        以上
      以上

$ '闭月' "结束阶段，你摸一张牌。"
  触发技
  时机:阶段开始时
  效果:若你的"当前阶段"是'结束阶段'，则你摸1张牌，以上。

$ '测试' "测试用技能"
  触发技
  时机: 游戏开始时
  效果: 你获得100枚"曹操"标记
  时机: 阶段结束时
  效果: 若你的"当前阶段"是'出牌阶段'且(你"曹操"标记数量)大于0，则你失去1枚"曹操"标记，你摸1张牌，以上。

$ '狼袭' "准备阶段，你可以对体力值不大于你的一名其他角色造成2点伤害。"
  触发技
  时机: 阶段开始时
  条件: 若你的"当前阶段"是'准备阶段'，且(你拥有技能'狼袭')则
         返回 真
       以上
  效果: 令'可选列表'为'其他角色'
       对'可选列表'中每个'p'重复此流程:
         若'p'的"体力值"大于你的"体力值"则
           从'可选列表'删除'p'
         以上
       以上
       若'可选列表'的"长度"大于0，则
         令'打击对象'为(你从'可选列表'选择一名角色)
         你对'打击对象'造成2点伤害
       以上

拓展包 '标'
# 魏 "魏武帝" '曹孟德' 4 女性 {moucaocao}
  [ "奸雄" ]

# 吴 "大嘟嘟" '标周瑜' 4
  [ "英姿" "闭月" ]

# 群 "测试工具人" '丁真' 7 中性 {test_dingzhen}
  [ "测试" "狼袭" ]
