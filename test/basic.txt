定义函数 '自定义函数'
  {
    '玩家': 玩家类型,
    '血量': 数字类型,
    '数量': 数字类型 = 5
  }
  令'random'为(调用'生成随机数'{})
  若 '玩家'的"体力值"小于'血量'则
    '玩家'摸'数量'张牌
  以上
  令X为1
  若X是1，则
    '玩家'摸1张牌。
  否则若X是2，则
    '玩家'回复1点体力。
  否则若X是3，则
    '玩家'失去1点体力。
  否则
  以上。

定义函数 'test2' {'a':数字类型=2,'b':数字类型} -> 数字类型
  返回 'a' + 'b'
  注：你摸一张牌
  注：返回语句后不允许写新的语句，它标志着语句块的结束

$ '奸雄' "锁定技。当你造成伤害后，若你的体力值大于3，你摸5张牌；当你受到伤害后，你对伤害来源造成一点伤害，重复此流程直到伤害来源的体力值小于你的体力值；你造成的伤害+2。"
  锁定技 触发技
  时机:造成伤害后
  效果:若你的"体力值"大于3，则你摸5张牌，以上。

  时机:受到伤害后
  效果:重复此流程:你对'伤害来源'造成1点伤害。直到'伤害来源'的"体力值"小于你的"体力值"。

  时机:造成伤害时
  效果:令'伤害值'为'伤害值'+2。调用'自定义函数'{'玩家':你,'血量':2}
  令X为(调用'test2'{'b':4})
  对[你, '伤害来源', '伤害目标']中每个'p'重复此流程:'p'摸1张牌。以上。

$ '英姿' "摸牌阶段，你多摸一张牌。" 锁定技 {test_yingzi}
  触发技
  时机:摸牌阶段摸牌时
  效果:令'摸牌数量'为'摸牌数量'+1。
      令'选项'为["摸一张牌，并对一名其他角色造成2点伤害","摸两张牌"]，
      令'选择结果'为(你从'选项'选择一项)，
      若'选择结果'不是"摸两张牌"则
        你摸1张牌
        你对(你从'其他角色'选择一名角色)造成2点伤害
      否则
        你摸2张牌
      以上

$ '闭月' "结束阶段，你摸一张牌。"
  触发技
  时机:阶段开始时
  效果:若你的"当前阶段"是'结束阶段'，则你摸1张牌，以上。

$ '测试' "测试用技能"
  触发技
  时机: 游戏开始时
  效果: 你获得100枚"曹操"标记
  时机: 阶段结束时
  效果: 若你的"当前阶段"是'出牌阶段'且(你"曹操"标记数量)大于0，则你失去1枚"曹操"标记，你摸1张牌，以上。
  若(你拥有技能"奸雄")则以上

$ '狼袭' "准备阶段，你可以对体力值不大于你的一名其他角色造成2点伤害。"
  触发技
  时机: 阶段开始时
  条件: 若你的"当前阶段"是'准备阶段'，且(你拥有技能"狼袭")则
         返回 真
       以上
  效果: 令'可选列表'为'其他角色'
       对'可选列表'中每个'p'重复此流程:
         若'p'的"体力值"大于你的"体力值"则
           从'可选列表'删除'p'
         以上
       以上
       若'可选列表'的"长度"大于0，则
         令'打击对象'为(你从'可选列表'选择一名角色)
         令X为(调用'生成随机数'{'上界':2,'下界':0})
         你对'打击对象'造成X点伤害
       以上

$ '崩坏' "锁定技，结束阶段，若你的体力值不是全场最低的，你选择：失去一点体力或失去一点体力上限。"
  锁定技
  触发技
  时机: 阶段开始时
  条件: 若你的"当前阶段"是'结束阶段'，且(你拥有技能"崩坏")则
         返回 真
       以上
  效果:  令'最低'为真
        对'其他角色'中每个'p'重复此流程:
          若'p'的"体力值"不大于你的"体力值"则
           令'最低'为假
           中止此流程
          以上
        以上
        若'最低'是假则
          令'选择'为(你从["体力","体力上限"]选择一项)
          若'选择'是"体力"则
            你失去1点体力
          否则
            你失去1点体力上限
          以上
        以上

$ '生有' "出牌阶段，你可以摸一张牌。" 普通技 {test_shengyou}
  主动技
  条件: 返回真
  选牌规则: 返回假
  选目标规则: 返回假
  可以点确定: 返回真
  使用后: 你摸1张牌。
    你说出"生有"的台词。
    你说出"生有"的台词{'音频编号':1}。

$ '制衡' "标孙权那个制衡"
  主动技
  条件: 注：若(你"制衡发动过次数"隐藏标记数量)不大于0，则返回真，以上。
  若(你发动主动技"制衡"的次数)不大于0，则返回真，以上。
  选牌规则: 返回真
  选目标规则: 返回假
  可以点确定: 若'已选卡牌'的"长度"大于0，则返回真，以上。
  使用后: 你因技能"制衡"弃置卡牌'选择的卡牌'。
注：  你获得1枚"制衡发动过次数"隐藏标记
  你摸'选择的卡牌'的"长度"张牌。

注：这是获取技能发动次数之前，制衡的实现版本。
注：这个例子也算是演示了主动技和触发技同时出现在一个技能内部的用法吧
注：  触发技
注：  时机: 阶段结束时
注：  效果: 若你的"当前阶段"是'出牌阶段'，则
注：    令X为(你"制衡发动过次数"隐藏标记数量)，
注：    你失去X枚"制衡发动过次数"隐藏标记。
注：  以上

$ '强袭' "出牌阶段限4次，你可以弃置一张非基本牌或者失去一点体力，对一名其他角色造成一点伤害。"
  主动技
  条件: 若(你发动主动技"强袭"的次数)不大于3，则返回真，以上。
  选牌规则: 若'备选卡牌'的"类别"不是'基本牌'，则返回真，以上。
  选目标规则: 若'已选目标'的"长度"是0，且'备选目标'不是你，则返回真，以上。
  可以点确定: 若'已选目标'的"长度"大于0，则返回真，以上。
  使用后:  若'选择的卡牌'的"长度"是0，则
            你失去1点体力，
          否则
            你因技能"强袭"弃置卡牌'选择的卡牌'。
          以上。
          然后你对('选择的目标'第1个元素)造成1点伤害
          你说出"Hello, world!"
          你发送战报"%from 发动了 “强袭”，效果拔群！原本可以造成 %arg 点伤害的。" {
            '%from': 你,
            '%arg': 6
          }

$ '赤练' "当你造成伤害时，你可以令目标角色弃置一张手牌。若你的体力不大于3，则改为弃两张牌。"
  锁定技
  触发技
  时机: 造成伤害后
  效果: 若你的"体力值"大于3，则
        '伤害目标'需弃置1张牌 {
          '可以弃装备': 假,
          '提示信息': (调用'创建提示信息' {
            '文本': "你犯了大吴疆土，请弃置 %arg 张牌",
            '变量1': 1
          })
        }，
       否则
         '伤害目标'需弃置2张牌{'技能名':"赤练"}，
         你洗牌。
       以上。

$ '鸽子' "出牌阶段，你可以与一名角色交换座位，然后变身为孙策。"
  主动技
  条件: 返回真
  选牌规则: 返回假
  选目标规则: 若'已选目标'的"长度"是0，且'备选目标'不是你，则返回真，以上。
  可以点确定: 若'已选目标'的"长度"大于0，则返回真，以上。
  使用后:
    你与('选择的目标'第1个元素)交换座位。
    你变身为"sunce"。
    注： 应输入要变成的武将的内部标识符。

$ '刚烈' "你受到伤害后，可以令伤害来源进行判定，若结果不为红桃，则伤害来源需弃置两张手牌，否则你对其造成一点伤害。"
  触发技
  时机: 受到伤害后
  条件: 若你是'伤害目标'，且'伤害来源'不是不存在的，且(你拥有技能"刚烈")，则返回真，以上。
  效果: 若(你选择发动"刚烈")，则
          你说出"nosganglie"的台词   注：nosganglie就是神杀自带的标刚烈，偷个语音用用
          令'判定牌'为(你进行判定{
            '技能名': "刚烈",
            '判定规则': (调用'创建卡牌规则'{
              '花色表': ['红桃']
            }),
            '希望判定中': 假
          })，
          若'判定牌'的"花色"不是'红桃'，则
            令'弃置的牌'为('伤害来源'弃置2张牌{
              '可以弃装备':假,
              '可以点取消':真,
              '提示信息': (调用'创建提示信息' {
                '文本': "%src 发动了“刚烈”，请弃置 %arg 张牌，否则你将受到 1 点伤害",
                '玩家1': 你,
                '变量1': 2
              })
            })
            若'弃置的牌'的"长度"是0，则
              你对'伤害来源'造成1点伤害。
            以上。
          以上。
        以上。

$ '乱击' "你可以将两张花色相同的牌当万箭齐发使用。"
  视为技
  条件: 返回真
  选牌规则: 若'已选卡牌'的"长度"是1，则
    若'备选卡牌'的"花色"是('已选卡牌'第1个元素)的"花色"，则返回真，以上。
    否则 若'已选卡牌'的"长度"是0，则返回真，以上。
    以上。
  可以点确定: 若'已选卡牌'的"长度"是2，则返回真，以上。
  视为规则: 返回(调用'创建虚拟牌'{
    '牌名': '万箭齐发',
    '子卡牌': '选择的卡牌',
    '技能名': "乱击"
  })

$ '倾国' "可以将黑色牌当闪使用打出"
  视为技
  条件: 返回假
  选牌规则: 若'已选卡牌'的"长度"是0，则返回'备选卡牌'的"花色"是'黑桃'或'备选卡牌'的"花色"是'梅花'，以上。
  可以点确定: 若'已选卡牌'的"长度"是1，则返回真，以上。
  视为规则: 返回(调用'创建虚拟牌'{
    '牌名': '闪',
    '子卡牌': '选择的卡牌',
    '技能名': "倾国"
  })
  响应条件: 返回真
  响应规则: [(调用'创建卡牌规则'{
    '牌名表': ['闪'],
    '花色表': ['黑桃', '梅花']
  })]

$ '龙胆' "你可以将一张【杀】当【闪】使用或打出，或将一张【闪】当普通【杀】使用或打出。"
  视为技
  条件: 返回真
  选牌规则: 若'已选卡牌'的"长度"是0，则
    令'名字'为'备选卡牌'的"牌名"，
    返回'名字'是'杀'或'名字'是'雷杀'或'名字'是'火杀'
      或'名字'是'闪'
  以上。
  可以点确定: 若'已选卡牌'的"长度"是1，则返回真，以上。
  视为规则:
    令'name'为('选择的卡牌'第1个元素)的"牌名"，
    若'name'是'闪'，则
      返回(调用'创建虚拟牌'{
        '牌名': '杀',
        '子卡牌': '选择的卡牌',
        '技能名': "龙胆"
      })
    否则
      返回(调用'创建虚拟牌'{
        '牌名': '闪',
        '子卡牌': '选择的卡牌',
        '技能名': "龙胆"
      })
    以上。
  响应条件: 返回真
  响应规则: ['杀', '闪']

$ '观星' "回合开始前，你可以观看牌堆顶五张牌，并按任意顺序调整之。"
  触发技
  时机: 阶段开始时
  条件: 若你的"当前阶段"是'准备阶段'，且(你拥有技能"观星")则
           返回 真
         以上
  效果: 若(你选择发动"观星")，则令'观星的牌'为(你选择牌堆顶5张牌{'是否不放回': 假})，
       你对'观星的牌'进行观星{'观星类型': '只放置顶部'}，以上。

$ '报警' "锁定技，当你的判定完成后，你摸一张牌。"
  锁定技
  触发技
  时机: 判定完成后
  条件: 若'判定角色'是你，且(你拥有技能"报警")，则返回真，否则返回假，以上。
  效果: 你摸1张牌。

注：$ '贞烈' "当你判定牌亮出后，你可以将牌堆顶的一张牌取代之作为判定结果。"
注：  触发技
注：  时机: 改判前
注：  条件: 若'判定角色'是你，且(你拥有技能"贞烈")，且"当前角色"是你，则返回真，否则返回假，以上。
注：  效果: 若(你选择发动"贞烈")，则令'改判的牌'为(你选择牌堆顶1张牌)，
注：       你将判定结果修改为('改判的牌'第1个元素){'技能名': "贞烈", '是否交换': 真}。以上。
$ '贞烈' "出牌阶段，你可以要求一名角色对自己使用一张牌。"
  主动技
  条件: 返回真
  选牌规则: 返回假
  选目标规则: 若'已选目标'的"长度"是0，且'备选目标'不是你，则返回真，以上。
  可以点确定: 若'已选目标'的"长度"大于0，则返回真，以上。
  使用后:
    令'p1'为(你选择自己的一张牌)
    令'目标'为('选择的目标'第1个元素)，
    '目标'获得牌'p1'
    你使用一张牌。
    令'牌'为(你选择'目标'一张牌)
    你获得牌'牌'

$ '反馈' "当你受到伤害时，你可以抽取伤害来源的一张牌。"
  触发技
  时机: 受到伤害后
  条件: 若你是'伤害目标'，且'伤害来源'不是不存在的，且(你拥有技能"反馈")，则返回真，以上。
  效果: 若(你选择发动"反馈")，则
    令'牌'为(你选择'伤害来源'一张牌{
      '位置': ['手牌区', '装备区', '判定区']
    })
    你获得牌'牌'
  以上。


$ '黑颜' "锁定技，你的红桃牌视为黑桃牌。" 锁定技
  状态技
  选牌规则: 若'备选卡牌'的"花色"是'红桃'，则返回真，以上。
  视为规则: 返回(调用'创建虚拟牌'{
    '牌名': '备选卡牌'的"牌名",
    '花色': '黑桃',
    '技能名': "黑颜",
    '子卡牌': [ '备选卡牌' ]
  })

$ '咆哮' "锁定技，你的牌视为雷杀。你使用杀无次数和距离限制，并且可以多指定三个目标。"
  状态技
  选牌规则: 返回真。
  视为规则: 返回(调用'创建虚拟牌'{
    '牌名': '雷杀',
    '技能名': "咆哮",
    '子卡牌': [ '备选卡牌' ]
  })

  出牌次数修正: 若('玩家'拥有技能"咆哮")，
    且('卡牌'的"牌名"是'杀'或'卡牌'的"牌名"是'雷杀'或'卡牌'的"牌名"是'火杀')，
    则 返回999，以上。   注：也就是把出牌次数+999
  出牌距离修正: 若('玩家'拥有技能"咆哮")，
    且('卡牌'的"牌名"是'杀'或'卡牌'的"牌名"是'雷杀'或'卡牌'的"牌名"是'火杀')，
    则 返回999，以上。   注：也就是把出牌指定距离的范围+999
  出牌目标数量修正: 若('玩家'拥有技能"咆哮")，
    且('卡牌'的"牌名"是'杀'或'卡牌'的"牌名"是'雷杀'或'卡牌'的"牌名"是'火杀')，
    则 返回3，以上。   注：也就是把出牌可以指定的目标数量+3

$ '移测' "卡牌移动测试"
  主动技
  条件: 返回真
  选牌规则: 返回真
  选目标规则: 若'已选目标'的"长度"是0，且'备选目标'不是你，则返回真，以上。
  可以点确定: 若'已选目标'的"长度"大于0，则返回真，以上。
  使用后:
    令'目标'为('选择的目标'第1个元素)
    你与'目标'换牌。
    令'拼点结果'为(你与'目标'拼点)。
    注：拼点有可能双方都没赢，所以不可以直接使用以下句子
    注：'拼点结果'的"获胜者"摸3张牌。
    若'拼点结果'的"获胜者"不是不存在的，则
      '拼点结果'的"获胜者"摸3张牌。
    以上。
    你获得牌'拼点结果'的"目标卡牌"
    你获得牌'拼点结果'的"来源卡牌"
    你交给'目标'牌['拼点结果'的"目标卡牌"]
    你弃置牌['拼点结果'的"来源卡牌"]
    调用'移动卡牌'{
      '移牌信息列表': [
        (调用'创建卡牌移动信息' {
          '卡牌列表': '选择的卡牌',
          '移动目标区域': '弃牌堆'
        })
      ]
    }

$ '阶测' "受到伤害后，可以翻面并进行额外的回合。跳过判定和弃牌"
  触发技
  时机:受到伤害后
  效果:若(你选择发动"阶测")则
    你翻面
    你进行额外回合
  以上

  时机:阶段开始时
  条件:若你的"当前阶段"是'准备阶段'，且(你拥有技能"阶测")，则返回真，以上
  效果:你跳过'判定阶段'，你跳过'弃牌阶段'

拓展包 'fkparse测试专用'
# 魏 "魏武帝" '曹孟德' 4 女性 {moucaocao}
  [ "奸雄" ]

# 吴 "大嘟嘟" '标周瑜' 4
  [ "英姿" "闭月" ]

# 群 "测试工具人" '丁真' 7 中性 {test_dingzhen}
  [ "测试" "狼袭" "崩坏" ]

# 吴 "吴王光耀" '谋孙权' 4
  [ "生有" "制衡" "强袭" ]

# 蜀 "测试工具人2" '小约翰可汗' 4 中性 {test_xiaoyuehankehan}
  [ "赤练" "鸽子" "观星" ]

# 魏 "测试工具人3" '冰川镜华' 3 女性 {test_hikawakyoka}
  [ "刚烈" "报警" "贞烈" "反馈" ]

# 神 "工具人3" 'ＺＹ' 4
  [ "乱击" "倾国" "龙胆" ]

注：新功能 - 添加已有技能
注：# 吴 "传说武将" '超级黄盖' 4
注：  [ "noskurou" "zhaxiang" "buyi" ]

拓展包 'fkp-zy'

# 神 "鸽王" 'zy' 4
  [ "咆哮" "移测" "阶测" ]
